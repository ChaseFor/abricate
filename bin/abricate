#!/usr/bin/env perl

use warnings;
use strict;
use File::Spec;
use Data::Dumper;
use FindBin;
use File::Spec;
use File::Slurp;
use List::MoreUtils qw(zip);
use Cwd qw(abs_path);
#use lib "$FindBin::RealBin/../perl5";

#..............................................................................
# Command line options

my $VERSION = "0.5";
my $EXE = just_filename( $FindBin::RealScript );

my(@Options, $debug, $quiet, $list, $summary, $check,
             $datadir, $db, $minid,
             $noheader, $csv, $nopath);
setOptions();

#..............................................................................
# Globals

my $OUTSEP = "\t";
my $ABSENT = '.';
my $FIELD = '%COVERAGE';
my $FIELDSEP = ';';
my $IDSEP = '~~~';

my $CULL = 1;   # BLAST -culling_limit 

my @BLAST_FIELDS = qw(
  qseqid qstart qend qlen
  sseqid sstart send slen sstrand
  evalue length pident gaps gapopen
);

my @COLUMNS = qw(
 FILE SEQUENCE START END GENE COVERAGE COVERAGE_MAP GAPS %COVERAGE %IDENTITY DATABASE ACCESSION
);
$COLUMNS[0] = '#'.$COLUMNS[0];

my @REQUIRE = qw(blastn makeblastdb seqret gzip unzip);

#..............................................................................
# Option parsing

if ($summary) {
  summary_table( \*ARGV );
  exit;
}

if ($list) {
  list_databases($datadir);
  exit;
}

if ($check) {
  $debug=1;
  msg("Checking dependencies are installed:");
  require_exe( @REQUIRE );
  msg("OK.");
  exit;
}

$OUTSEP = ',' if $csv;  # default is tab

# check if dependencies are installed
require_exe( @REQUIRE );

# check if blastn > 2.2.30 to support 'gaps' custom field
my($version) = qx(blastn -version 2>&1);
$version =~ m/2.(\d+.\d+)\+/ or err("Could not parse blastn version from '$version'");
$1 >= 2.30 or err("You need to install BLAST+ 2.2.30 or higher");

# check database exists
$datadir = abs_path($datadir);
my $db_path = "$datadir/$db/sequences";
my $dbinfo = blast_database_info($db_path);
$dbinfo or err("BLASTN database not found: $db_path");
msg("Using database $db: ", $dbinfo->{SEQUENCES}, "sequences - ", $dbinfo->{DATE});

# output format
my $format = "6 @BLAST_FIELDS";
print line(@COLUMNS) unless $noheader;

FILE:
for my $file (@ARGV) {
  my %seen;
  my @hit;
  msg("Processing: $file");
  if (! -r $file) {
    msg("WARNING: '$file' does not exist, or is unreadable");
    next FILE;
  }
  my $cmd = "(gzip -d -c -f \Q$file\E | seqret -auto -filter -osformat2 fasta |"
          . " blastn -db \Q$db_path\E -outfmt '$format'"
          . " -task blastn -evalue 1E-20 -dust no -max_target_seqs 10000 -perc_identity $minid"
          . " -culling_limit $CULL) 2>&1"
          ;
  msg("Running: $cmd") if $debug;
  
  open BLAST, "$cmd |";
  while (<BLAST>) { 
    chomp;
    my @x = split m/\t/;
    if (@x != @BLAST_FIELDS) {
      msg("WARNING: can not find sequence data in '$file'");
      next FILE;
    }
    my %hit = (map { $BLAST_FIELDS[$_] => $x[$_] } 0 .. @BLAST_FIELDS-1); 
    ($hit{sstart}, $hit{send}) = ($hit{send}, $hit{sstart}) if $hit{sstrand} eq 'minus';
    next if $seen{ join('~', @hit{qw(qseqid qstart qend)}) }++;
#    $hit{sseqid} =~ s/_.*$//;
    my($database, $gene, $acc) = split m/$IDSEP/, $hit{sseqid};
    # if it wasn't in the format we expected, try and be sensible
    if (!defined $gene and !defined $acc) {
      $acc = '';
      $gene = $hit{sseqid};
      $database = $db;  # name specified with --db
    }
    msg( Dumper(\%hit) ) if $debug;
    my $minimap = minimap( @hit{qw(sstart send slen gapopen)} );
#    $minimap .= '!-'.$hit{gaps} if $hit{gaps} > 0;
    push @hit, [ 
      $nopath ? just_filename($file) : $file ,
      $hit{qseqid}, 
      $hit{qstart}, 
      $hit{qend},
      $gene,  #$hit{sseqid},
      $hit{sstart}.'-'.$hit{send}.'/'.$hit{slen}, 
      $minimap, 
      $hit{gapopen}.'/'.$hit{gaps},
      sprintf("%.2f", 100 * ($hit{length}-$hit{gaps}) / $hit{slen}),
      sprintf("%.2f", $hit{pident}),
      $database,
      $acc,
    ];
  }
  close BLAST;

  msg("Found", scalar(@hit), "genes in $file");

  # Sort hits
  #0    1   2     3   4    5
  #FILE CHR START END GENE COVERAGE COVERAGE_MAP GAPS %COVERAGE %IDENTITY DATABASE ACCESSION
#  @hit = sort { $a->[4] cmp $b->[4] || $b->[8] <=> $a->[8] } @hit;
  @hit = sort { $a->[1] cmp $b->[1] || $a->[2] <=> $b->[2] } @hit;

  print line(@$_) for @hit;  
}

#----------------------------------------------------------------------

sub minimap {
  my($x, $y, $L, $broken, $scale, $on, $off) = @_;
  my $WIDTH = 15 - ($broken ? 1 : 0);
  $broken ||= 0;
  $scale ||= ($L/$WIDTH);
  $on  ||= '=';
  $off ||= '.';
  $x = int( $x / $scale );
  $y = int( $y / $scale );
  $L = int( $L / $scale );
  my $map='';
  for my $i (0 .. $WIDTH-1) {
#    msg("$i $x $y $L $scale");
    $map .= ($i >= $x and $i <= $y) ? $on : $off;
    $map .= '/' if $broken and $i==int($WIDTH/2);
  }
  return $map;
}

#----------------------------------------------------------------------

sub summary_table {
  my($fh) = @_;
  
  my %gene;  # genes observed
  my %data = (map { $_ => {} } @ARGV);
  my @hdr;
  
  while (<$fh>) {
    chomp;
    my @col = split m/\t/;
    print STDERR "### [$.] @col\n" if $debug;
    @hdr = @col if $. == 1;
    next if $col[0] =~ m/^#/;
    $col[0] = just_filename($col[0]) if $nopath;
    my $row = { zip @hdr, @col };    
    $gene{ $row->{'GENE'} }++;
    push @{ $data{ $row->{'#FILE'} }{ $row->{'GENE'} } } , $row;
  }

  my @gene = sort { $a cmp $b } keys %gene;
  print line('#FILE', 'NUM_FOUND', @gene);

  for my $fname (sort { $a cmp $b } keys %data) {
    my @present = map { exists $data{$fname}{$_} 
                        ? join( $FIELDSEP, map { $_->{$FIELD} } @{$data{$fname}{$_}} ) 
                        : $ABSENT 
                      } @gene;
    print line( $fname, scalar( keys %{$data{$fname}} ), @present );
  }
}

#----------------------------------------------------------------------

sub blast_database_info {
  my($prefix) = @_;
  my(@res) = qx(blastdbcmd -info -db \Q$prefix\E 2> /dev/null);
  chomp @res;
  my $res = join(' ', @res);
  my $info = { PREFIX => $prefix };

  $res =~ m/\b([\d,]+)\s+sequences;/ or return;
  $info->{SEQUENCES} = $1;
  $info->{SEQUENCES} =~ s/,//g;

  $res =~ m/\bDatabase: (\S+)/;
  $info->{TITLE} = $1;
  
  $res =~ m/\bDate: (\w+\s+\d+,\s+\d{4})\s/;
  $info->{DATE} = $1;

  return $info;
}

#----------------------------------------------------------------------

sub list_databases {
  my($from) = @_;
  my @dir = grep { -d "$from/$_" } read_dir($from);
  my $count=0;
  for my $name (sort @dir) {
    # msg("Database: $name");
    my $info = blast_database_info("$from/$name/sequences");
    if ($info) {
      msg("$name: ", $info->{SEQUENCES}, "sequences - ", $info->{DATE});
    }
    else {
      msg("$name: corrupt database? - $from/$name");
    }
  }
}

#----------------------------------------------------------------------

sub line {
  return join($OUTSEP, @_)."\n";
}

#----------------------------------------------------------------------

sub version {
  print "$EXE $VERSION\n";
  exit;
}

#----------------------------------------------------------------------

sub msg {
  print STDERR "@_\n" unless $quiet;
}

#----------------------------------------------------------------------

sub err {
  print STDERR "ERROR: @_\n";
  exit(1);
}

#----------------------------------------------------------------------

sub just_filename {
  my($path) = @_;
  my(undef,undef,$fname) = File::Spec->splitpath( $path );
  return $fname;
}

#----------------------------------------------------------------------

sub require_exe {
  my(@arg) = @_;
  for my $exe (@arg) {
    my $where = '';
    for my $dir ( File::Spec->path ) {
      if (-x "$dir/$exe") {
        $where = "$dir/$exe";
        last;
      }
    }
    if ($where) {
      msg("Found '$exe' => $where") if $debug;
    }
    else {
      err("Could not find '$exe'. Please install it and ensure it is in the PATH.");
    }
  }
  return;
}

#----------------------------------------------------------------------
# Option setting routines

sub setOptions {
  use Getopt::Long;

  @Options = (
    {OPT=>"help",    VAR=>\&usage,             DESC=>"This help"},
    {OPT=>"debug!",  VAR=>\$debug, DEFAULT=>0, DESC=>"Verbose debug output"},
    {OPT=>"quiet!",  VAR=>\$quiet, DEFAULT=>0, DESC=>"Quiet mode, no stderr output"},
    {OPT=>"version!",  VAR=>\&version, DESC=>"Print version and exit"},
    {OPT=>"list!",  VAR=>\$list, DEFAULT=>0, DESC=>"List included databases"},
    {OPT=>"check!",  VAR=>\$check, DEFAULT=>0, DESC=>"Check dependencies are installed"},
    {OPT=>"summary!",  VAR=>\$summary, DEFAULT=>0, DESC=>"Summarize multiple reports into a table"},
    {OPT=>"datadir=s",  VAR=>\$datadir, DEFAULT=>"$FindBin::RealBin/../db", DESC=>"Location of database folders"},
    {OPT=>"db=s",  VAR=>\$db, DEFAULT=>"resfinder", DESC=>"Database to use"},
    {OPT=>"noheader!",  VAR=>\$noheader, DEFAULT=>0, DESC=>"Suppress column header row"},
    {OPT=>"csv!",  VAR=>\$csv, DEFAULT=>0, DESC=>"Output CSV instead of TSV"},
    {OPT=>"minid=f",  VAR=>\$minid, DEFAULT=>75, DESC=>"Minimum DNA identity"},
    {OPT=>"nopath!",  VAR=>\$nopath, DEFAULT=>0, DESC=>"Strip filename paths from FILE column"},
  );

  @ARGV or usage();

  &GetOptions(map {$_->{OPT}, $_->{VAR}} @Options) || usage();

  # Now setup default values.
  foreach (@Options) {
    if (defined($_->{DEFAULT}) && !defined(${$_->{VAR}})) {
      ${$_->{VAR}} = $_->{DEFAULT};
    }
  }
}

sub usage {
  print "Synopsis:\n";
  print "\tFind and collate amplicons in assembled contigs\n";
  print "Usage:\n";
  print "\t% $EXE --list\n";
  print "\t% $EXE [options] <sequence.{fasta,gbk,embl}[.gz]> > out.tab\n";
  print "\t% $EXE --summary <out1.tab> <out2.tab> <out3.tab> ... > summary.tab\n";
  print "Options:\n";
  foreach (@Options) {
    printf "\t--%-13s %s%s.\n",$_->{OPT},$_->{DESC},
           defined($_->{DEFAULT}) ? " (default '$_->{DEFAULT}')" : "";
  }
  exit(1);
}
 
#----------------------------------------------------------------------
